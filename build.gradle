// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlinVersion = '1.1.2-4'
    ext.gradleVersion = '2.3.2'
    project.extensions.extraProperties.set('gradleVersion', gradleVersion)
    project.extensions.extraProperties.set('kotlinVersion', kotlinVersion)

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath 'me.tatarka:gradle-retrolambda:3.3.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.google.gms:google-services:3.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    minSdkVersion = 19
    targetSdkVersion = 25
    compileSdkVersion = 25
    buildToolsVersion = '25.0.3'

    rxJavaVersion = '2.0.7'
    rxAndroidVersion = '2.0.1'
    rxKotlinVersion = '2.0.3'
    rxFirebaseVersion = '10.2.0.0'
    rx = {
        compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
        compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
        compile "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"
        compile "com.androidhuman.rxfirebase2:firebase-database:$rxFirebaseVersion"
    }

    daggerVersion = '2.8'
    dagger = {
        compile "com.google.dagger:dagger:$daggerVersion"
        kapt "com.google.dagger:dagger-compiler:$daggerVersion"
        provided "org.glassfish:javax.annotation:10.0-b28"
    }

    androidSupportVersion = '25.3.1'
    androidSupport = {
//        compile "com.android.support:appcompat-v7:$androidSupportVersion"
        compile "com.android.support:recyclerview-v7:$androidSupportVersion"
        compile "com.android.support:cardview-v7:$androidSupportVersion"
        compile "com.android.support:palette-v7:$androidSupportVersion"
        compile "com.android.support:design:$androidSupportVersion"
        compile "com.android.support:support-vector-drawable:$androidSupportVersion"
    }

    firebaseVersion = '10.0.1'
    firebase = {
        compile "com.google.firebase:firebase-database:$firebaseVersion"
    }

    gradleVersion = project.extensions.extraProperties.get('gradleVersion')
    androidBind = {
        kapt "com.android.databinding:compiler:$gradleVersion"
    }

    testLibraries = {
        testCompile 'junit:junit:4.12'
        androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
            exclude group: 'com.android.support', module: 'support-annotations'
        })
    }
}
